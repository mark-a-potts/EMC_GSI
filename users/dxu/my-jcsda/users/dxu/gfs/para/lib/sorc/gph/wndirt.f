      SUBROUTINE WNDIRT(IDOTS,JDOTS, C2DIR,IDDGD,NSSS)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    WNDIRT      PLOTTING WIND DIRECTION
C   PRGMMR: KRISHNA KUMAR        ORG: W/NP12   DATE: 1999-07-01
C
C ABSTRACT:
C     ...TO PLOT WIND DIRECTION DIGIT NEAR END OF WIND STAFF;
C     ...BY LOCATING POSITION OF LITTLE WIND DIR DIGIT NR 
C        END OF WND STAFF, AND PLOTTING THE 2ND DIGIT OF NDD AT 
C        THAT POSITION
C
C PROGRAM HISTORY LOG:
C   ??-??-??  AUTHOR UNKNOWN
C   93-04-05  LILLY CONVERT SUBROUTINE TO FORTRAN 77.
C   96-10-17  SHIMOMURA - CONVERT TO CRAY FORTRAN
C 1999-07-01  KRISHNA KUMAR  CONVERTED THIS CODE FROM CRAY TO IBM
C             RS/6000.
C
C USAGE:    CALL WNDIRT(IDOTS,JDOTS,NDIR,IDDGD,NSSS)
C   INPUT ARGUMENT LIST:
C     ...GIVEN
C     (1), (2) IDOTS,JDOTS -- LOCATION OF STN IN DOTS (NEAREST
C                                       HUNDREDTH OF AN INCH)
C     (3) C2DIR  -- CHARACTER*2 TRUE WIND DIR IN TENS OF DEGREES
C                                     ('01' THRU '36', OR '99' IF CALM)
C     (4) IDDGD -- GRID ORIENTED WIND DIR (I2) IN TENS OF DEGREES
C     (5) NSSS  -- WIND SPEED IN KTS (I3) (=0 IF.LT.5KTS)
C
C
C   OUTPUT FILES:  (DELETE IF NO OUTPUT FILES IN SUBPROGRAM)
C     FT55F001 - LABEL ARRAY WHERE PUTLAB PUTS THE CHARACTER
C     FT06F001 - INCLUDE IF ANY PRINTOUT
C
C REMARKS: LIST CAVEATS, OTHER HELPFUL HINTS OR INFORMATION
C     THE 3RD ARG: CHARACTER*2 C2DIR
C     MUST BE A CHARACTER STRING OF LENGTH AT LEAST 2-CHARACTERS
C     CONTAINING THE 2-DIGIT WIND DIRECTION
C     TO THE TENS OF DEGREES (IN ASCII)
C
C ATTRIBUTES:
C   LANGUAGE: F90
C   MACHINE:  IBM
C
C$$$
C
C
C USAGE:    CALL WNDIRT(IDOTS,JDOTS,C2DIR,IDDGD,NSSS)
      INTEGER        IDOTS,JDOTS
      CHARACTER*(*)  C2DIR
      INTEGER        IDDGD
      INTEGER        NSSS
C
C
      REAL    CONVTR
      DATA    CONVTR  	/0.174533/
C     ...CONVERTS TENS OF DEGREES TO RADIANS

      REAL    HYPKA
      DATA    HYPKA  	/31.0/

      REAL    HYPKB
      DATA    HYPKB	/37.0/

      REAL    ADXLL
      DATA    ADXLL	/-3.5/

      REAL    ADYLL
      DATA    ADYLL	/-5.0/
C     ...WHICH ARE INCREMENTS FORM CENTER OF FIGURE TO LL CORNER
C
      INTEGER  ILLDIG,JLLDIG
      INTEGER  IDDA
      REAL     DDA
      REAL     DIRAD
      REAL     DELX,DELY

      INTEGER   IROT_PRI(2)
      INTEGER   IPRIOR
      EQUIVALENCE (IROT_PRI(2),IPRIOR)
C
C
      SAVE
C
      IF(NSSS .LE. 0) THEN
        GO TO 800
      ENDIF

      IF(NSSS .GE. 48) THEN
        HYPOT=HYPKB
      ELSE
        HYPOT=HYPKA
      ENDIF

C     (3) C2DIR  -- C*2 TRUE WIND DIR (A2) IN TENS OF DEGREES
C                                   ('01' THRU '36', OR '99' IF CALM)
      LENDIR = LEN(C2DIR)
      IF(LENDIR .LT. 2) THEN
        WRITE(6,123)
  123   FORMAT(1H ,'WNDIRT:ERROR IN GIVEN LEN OF ARG3: C2DIR;',
     1        /1H ,7X,'CALLER MUST DEFINE AS CHARACTER STRING ',
     2                ' OF LEN AT LEAST *2')
        GO TO 999
      ENDIF
C     ... OTHERWISE, C2DIR IS A CHARACTER STRING WITH LEN .GE. 2;
      IF(C2DIR .EQ. '99') THEN
        GO TO 800		!... CALM WIND; DO NOTHING EXIT
      ENDIF

      IDDA = 45 - IDDGD
      IF(IDDA .GE. 36) THEN
        IDDA = IDDA - 36
      ENDIF
      DDA   = IDDA + 1
C     ...PLOTTED DIGIT TEN DEGREES AWAY FROM STAFF
      DIRAD = DDA*CONVTR
      DELX  = HYPOT*COS(DIRAD)
      DELY  = HYPOT*SIN(DIRAD)
      ILLDIG = NINT(FLOAT(IDOTS) + DELX + ADXLL)
      IF(ILLDIG .LE. 0) THEN
        GO TO 810
      ENDIF

      JLLDIG = NINT(FLOAT(JDOTS) + DELY + ADYLL)
      IF(JLLDIG .LE. 0) THEN
        GO TO 810
      ENDIF
C     ...THAT FINISHES POSITIONING OF DIGIT
      IPRIOR=1
      IF(C2DIR(2:2) .GE. '0' .AND. C2DIR(2:2) .LE. '9') THEN

        CALL PUTLAB(ILLDIG,JLLDIG,19.0,C2DIR(2:2),0.0,1,IROT_PRI,0)

      ELSE
        WRITE(6,335)C2DIR(1:2)
  335   FORMAT(1H ,'WNDIRT:UNABLE TO PLOT WND-DIR DIGIT BECAUSE ',
     1             'GIVEN ILLEGAL C2DIR-VALUE="',A2,'"')
        GO TO 999
       
      ENDIF

      RETURN
C     ...WHICH IS NORMAL EXIT
C
  800 CONTINUE
C     ...COMES TO 800 FOR CALM WIND. NOTHING PLOTTED.
      RETURN

  810 CONTINUE
C     ...COMES TO 810 FOR NEG VALUED I OR J, SO OFF GRID-EXIT
      PRINT 811
  811 FORMAT(1H ,'WNDIRT: NEGATIVE-VALUED I/J SO POINT OFF-GRID')
      RETURN

  999 CONTINUE
      RETURN
      END
