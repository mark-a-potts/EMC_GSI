       INTEGER FUNCTION IRVR(DATA_WORD,LEN_BITS)
C                                             17-JAN-1996/DSS
C      ... TO REVERSE THE BIT SEQUENCE OF THE HI-ORDER "LEN_BITS" BITS
C      ...    IN THE GIVEN "DATA" WORD.
C      ... THE RESULTING WORD CONTAINS ZERO-FILL TO THE RIGHT OF 
C      ...    "LEN_BITS" BITS
C      ... CALLED FROM PRTITL()
C      ... CONVERTING FROM .ASM TO CRAY FORTRAN
C
       INTEGER  DATA_WORD       	!... GIVEN DATA TO BE REVERSED
       INTEGER  LEN_BITS    		!... COUNT OF BITS TO BE REVERSD

C      ... TRANSLATE TABLE TO REVERSE THE BITS WITHIN EACH BYTE ...
       INTEGER       TABLE(32)    
       CHARACTER*1  CTABLE(256)
       EQUIVALENCE (TABLE(1),CTABLE(1))

       DATA     TABLE  / X'008040C020A060E0', X'109050D030B070F0',
     1                   X'088848C828A868E8', X'189858D838B878F8',
     2                   X'048444C424A464E4', X'149454D434B474F4',
     3                   X'0C8C4CCC2CAC6CEC', X'1C9C5CDC3CBC7CFC',
     4                   X'028242C222A262E2', X'129252D232B272F2',
     5                   X'0A8A4ACA2AAA6AEA', X'1A9A5ADA3ABA7AFA',
     6                   X'068646C626A666E6', X'169656D636B676F6',
     7                   X'0E8E4ECE2EAE6EEE', X'1E9E5EDE3EBE7EFE',
     8                   X'018141C121A161E1', X'119151D131B171F1',
     9                   X'098949C929A969E9', X'199959D939B979F9',
     A                   X'058545C525A565E5', X'159555D535B575F5',
     B                   X'0D8D4DCD2DAD6DED', X'1D9D5DDD3DBD7DFD',
     C                   X'038343C323A363E3', X'139353D333B373F3',
     D                   X'0B8B4BCB2BAB6BEB', X'1B9B5BDB3BBB7BFB',
     E                   X'078747C727A767E7', X'179757D737B777F7',
     F                   X'0F8F4FCF2FAF6FEF', X'1F9F5FDF3FBF7FFF' /

       INTEGER      NBYTPWRD
       PARAMETER   (NBYTPWRD=8)    	!... CRAY VERSION

       INTEGER      MAXBITPWRD
       PARAMETER   (MAXBITPWRD=8*NBYTPWRD)

       INTEGER      JWORK
       CHARACTER*1  CWORK(NBYTPWRD)
       EQUIVALENCE (JWORK,CWORK(1))

       INTEGER      JACC
       CHARACTER*1  CACC(NBYTPWRD)
       EQUIVALENCE (JACC,CACC(1))

       INTEGER      IXVALUE
       INTEGER      NBYTES
       INTEGER      NDOBITS

C      . . . .   S T A R T   . . . . . . . . . . . . . . . . .

       JACC = 0
       JWORK = DATA_WORD
       NDOBITS = LEN_BITS

       IF(NDOBITS .LE. 0) THEN
         GO TO 999
       ELSE IF(NDOBITS .GT. MAXBITPWRD) THEN
         NDOBITS = MAXBITPWRD
       ENDIF
       
       NBYTES = NDOBITS / 8
       IF(MOD(NDOBITS,8) .NE. 0) THEN
         NBYTES = NBYTES + 1
       ENDIF

       DO  IC = 1,NBYTES
         IR = NBYTPWRD - IC + 1
         IXVALUE = mova2i(CWORK(IC))
         CACC(IR) = CTABLE(IXVALUE+1)
       ENDDO
       
       LEFSHF = MAXBITPWRD - NDOBITS
       IF(LEFSHF .GT. 0) THEN
         JACC = ISHFT(JACC,LEFSHF)
       ENDIF

  999  CONTINUE
       IRVR = JACC
       RETURN
       END
