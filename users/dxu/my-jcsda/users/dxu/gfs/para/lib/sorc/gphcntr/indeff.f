C*********************************************************************
      SUBROUTINE INDEFF (ZIN,IMZIN,JMZIN,Z,IMZ,JMZ,NDV,XINDEF)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: INDEFF    SET UNDEFINED POINTS
C   AUTHOR: ?           ORG: ?        DATE: ?
C
C ABSTRACT: INTERPOLATION PROCEDURE IGNORED THE PRESENCE OF UNDEFINED
C   POINTS.  REINTRODUCE UNDEFINED POINTS INTO NEW DATA FIELD.
C
C HISTORY LOG:
C   ??-??-??  ORIGINAL AUTHOR UNKNOWN
C   97-11-10  CHRIS CARUSO   MODIFY FOR CRAY
C   98-04-28  CHRIS CARUSO   UPGRADE TO F90.
C  1999-07-16 HENRICHSEN MODIFY TO RUN ON IBM SP.  
C
C USAGE:  CALL INDEFF (ZIN,IMZIN,JMZIN,Z,IMZ,JMZ,NDV,XINDEF)
C   INPUT ARGUMENTS:
C     ZIN      - ZIN(IMZIN,JMZIN) CONTAINS GRID DATA.
C     IMZIN    - I DIMENSION OF ZIN ARRAY.
C     JMZIN    - J DIMENSION OF ZIN ARRAY.
C     Z        - Z(IMZ,JMZ) WORKING ARRAY
C     IMZ      - I DIMENSION OF ZIN ARRAY.
C     JMZ      - J DIMENSION OF ZIN ARRAY.
C     NDV      - FURTHER DIVISIONS OF A GRID INTERVAL
C              - =2,3,4,5,6 INTERPOLATE ORIGINAL ONE GRID INTO (NDIV)
C              - SUBGRID AND TRACE EVERY CONTOUR LINE
C     XINDEF   - REAL VALUE OF UNDEFINED POINT
C
C   OUTPUT ARGUMENTS:
C     Z        - Z(IMZ,JMZ) WORKING ARRAY
C
C   SUBPROGRAMS CALLED:
C     NONE
C
C ATTRIBUTES:
C   LANGUAGE: IBM FORTRAN 90
C   MACHINE:  IBM
C
C$$$
      REAL ZIN(IMZIN,JMZIN), Z(IMZ,JMZ)
      REAL XINDEF
      INTEGER IMZIN, JMZIN, IMZ, JMZ, NDV
  
      SAVE

      DO J = 1,JMZIN
        DO I = 1,IMZIN
          IF(ZIN(I,J).EQ.XINDEF) THEN
            IBEG = MAX(   1   ,I-2)
            IEND = MIN(IMZIN-1,I+1)
            JJ   = (J-1)*NDV + 1

            DO ICOL = IBEG,IEND
              DO N = 1,NDV-1
                II = (ICOL-1)*NDV + 1 + N
                Z(II,JJ) = XINDEF
              ENDDO
            ENDDO
          ENDIF
        ENDDO
      ENDDO

      DO II = 1,IMZ
        DO JJROW = 1,JMZ,NDV
          IF(Z(II,JJROW).EQ.XINDEF) THEN
            JROW = (JJROW-1)/NDV + 1
            JBEG = MAX(1,JROW-2)
            JEND = MIN(JMZIN-1,JROW+1)   

            DO JROW = JBEG,JEND
              DO N = 1,NDV-1
                JJ       = (JROW-1)*NDV + 1 + N
                Z(II,JJ) = XINDEF
              ENDDO
            ENDDO
          ENDIF
        ENDDO
      ENDDO

      RETURN
      END
