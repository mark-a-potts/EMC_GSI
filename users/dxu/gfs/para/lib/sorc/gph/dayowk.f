C          DATA SET DAYOWK     AT LEVEL 001 AS OF 04/16/93
      SUBROUTINE DAYOWK(IDAY,IMONTH,IYEAR,IDAYWK,IHDAYW)
C$$$  SUBPROGRAM DOCUMENTATION  BLOCK
C                .      .    .                                       .
C SUBPROGRAM:  DAYOWK        FIND NUMERICAL DAY OF WEEK.
C   PRGMMR: HENRICHSEN       ORG: NP12       DATE:1999-08-20
C
C ABSTRACT: GIVEN DAY MONTH AND YEAR AS INTEGERS RETURN DAY OF WEEK AS
C   AND INTEGER NUMBER AND AS FOUR CHARACTER HOLLERTH TEXT.
C
C PROGRAM HISTORY LOG:
C   84-MM-DD  WICK
C   85-MM-DD  HENRICHSEN REMOVE OLD KEY PUNCH CHARACTERS AND CLEAN UP.
C   87-12-10  HENRICHSEN CONVERT TO VS FORTAN 77.
C 1999-08-20  HENRICHSEN CONVERT FROM CRAY TO IBM RS/6000.
C
C USAGE:    CALL DAYOWK(IDAY,IMONTH,IYEAR,IDAYWK,IHDAYW)
C   INPUT ARGUMENT LIST:
C     IDAY     - INTEGER TWO DIGET DAY OF MONTH( 1 THRU 31 ).
C     IMONTH   - INTEGER TWO DIGET MONTH OF YEAR( 1 THRU 12).
C     IYEAR    - INTEGER FOUR DIGET YEAR.
C                IF 2 DIGIT YEAR USED, ERROR MSG IS PRINTED
C                AND SUBOUTINE DOES AN IMMEDIATE RETURN.
C
C   OUTPUT ARGUMENT LIST:
C     IDAYWK   - INTEGER NUMBER OF DAY OF WEEK ( 1 THRU 7 ).
C     IHDAYW   - CHARACTER*4 WORD CONTAINING THE THE THREE LETTER
C              - DAY OF THE WEEK LEFT JUSTIFED IN THE WORD.
C              - IE 'MON ', .... 'FRI ', .... 'SUN '.
C     ERRFLAG  - ERROR OF OUT OF RANGE ARGUEMENT IS INDICATED BY THE
C              - RESULTING IDAYWK = 0, AND IHDAYW  = BLANKS.
C
C
C   OUTPUT FILES:  (DELETE IF NO OUTPUT FILES IN SUBPROGRAM)
C     FT06F001 - ERROR PRINT WHEN ARGUEMENTS OUT OF RANGE.
C
C REMARKS: NONE.
C
C ATTRIBUTES:
C   LANGUAGE: F90 
C   MACHINE:  IBM
C
C$$$
      CHARACTER*4   IHDAYW
      CHARACTER*4   ITEXT(2)
      CHARACTER*4   KHDAY(7)
      CHARACTER*4   KLANK
      CHARACTER*4   NHDAY(2)
      CHARACTER*4   NHMON(2)
      CHARACTER*4   NHYR(2)
C
      INTEGER   KCEN(5)
      INTEGER   KDAYS(12)
      INTEGER   MONTAB(12)
C
      DATA          KHDAY   /'SUN ','MON','TUE ',
     1                       'WED ','THU ','FRI ','SAT '/
      DATA          KLANK   /'    '/
      DATA          NHDAY   /'DAY ','    '/
      DATA          NHMON   /'MONT','H   '/
      DATA          NHYR    /'YEAR','    '/
C
      DATA        KCEN     /4,2,0,6,4/
      DATA        KDAYS    /31,29,31,30,31,30,31,31,30,31,30,31/
      DATA        MONTAB   /1, 4, 4, 0, 2, 5, 0, 3, 6, 1, 4, 6/
C
      IDAYWK = 0
      IHDAYW = KLANK
      ISFEB = 0
      IDA = IDAY
      IMO = IMONTH
      IYR = IYEAR
      IF(IMO) 911,911,122
  122 IF(IMO .GT. 12) GO TO 911
      IF(IDA) 922,922,133
  133 IF(IDA .GT. KDAYS(IMO)) GO TO 922
      ICENT = IYR / 100
      IYR2 = IYR - 100 * ICENT
      IF(ICENT) 933,140,144
  140 CONTINUE
C     ...ONLY LAST 2 DIGITS OF YR WERE GIVEN, THIS IS WRONG.  
C     PRINT ERROR MESSAGE AND RETURN.
      PRINT *, "2 DIGIT YEAR USED IN CALL TO DAYOWK; NOT Y2K COMPLIANT."
      PRINT *, "IYEAR=",IYEAR
      PRINT *, "SUBROUTINE DAYOWK REQUIRES 4 DIGIT YEAR."
      RETURN
  144 CONTINUE
      ICENTX = ICENT - 16
      IF(ICENTX) 933,933,155
  155 IF(ICENTX .GT. 5) GO TO 933
C     ...THE GIVEN DATE INFO IS W/I RANGE...
      ISUM = KCEN(ICENTX) + IYR2 +IYR2/4 + MONTAB(IMO) + IDA
      IF(IMO - 2) 222,211,611
C     ...OTHERWISE, THIS IS JAN OR FEB, SO CHECK FOR LEAP YR...
  211 ISFEB = 1
  222 CONTINUE
      IF(MOD(IYR,4)) 533,511,533
C     ...MOST LIKELY  A LEAP YR.  TEST FOR CENTURY YR...
  511 CONTINUE
      IF(IYR2)522,515,522
  515 CONTINUE
      IF(MOD(IYR,400))533,522,533
C     ...COMES TO 522 IF LEAP YR CORRECTION IS NEEDED...
  522 CONTINUE
      ISUM = ISUM - 1
      GO TO 611
  533 CONTINUE
C     ...COMES TO 533 IF NOT A LEAP YR, SO  IF FEB, MUST RETEST IDA...
      IF(ISFEB)544,611,544
  544 CONTINUE
C     ...THIS IS FEB OF A NON-LEAP YR.
      IF (IDA .GE. KDAYS(2)) GO TO 922
      GO TO 611
  611 CONTINUE
      IDAW = MOD(ISUM,7)
      IF(IDAW) 644,633,644
  633 CONTINUE
      IDAW = 7
      GO TO 644
  644 CONTINUE
      IDAYWK = IDAW
      IHDAYW = KHDAY(IDAW)
      RETURN
C
  911 CONTINUE
C     ...COMES HERE IF GIVEN MONTH OUT-OF-RANGE...
      ITEXT(1) = NHMON(1)
      ITEXT(2) = NHMON(2)
      GO TO 955
  922 CONTINUE
C     ...COMES HERE IF GIVEN DAY OUT-OF-RANGE
      ITEXT(1) = NHDAY(1)
      ITEXT(2) = NHDAY(2)
      GO TO 955
  933 CONTINUE
C     ...COMES HERE IF GIVEN YR OUT OF RANGE
      ITEXT(1) = NHYR(1)
      ITEXT(2) = NHYR(2)
      GO TO 955
  955 CONTINUE
      PRINT  956, ITEXT(1),ITEXT(2),IDAY,IMONTH,IYEAR
  956 FORMAT(1H0,10X,'ERROR EXIT FROM DAYOWK. GIVEN ', A4, A1, 1X,
     X       'OUT-OF-RANGE', /1H0,15X,'IDAY = Z', Z8, 4X,'IMONTH = Z',
     X       Z8, 4X, 'IYEAR = Z', Z8)
      RETURN
      END
