      SUBROUTINE TRULL(XI,XJ,ALAT,ALONG,KEIL)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:  TRULL         LAT/LONG FROM GRID I/J
C   PRGMMR: KRISHNA KUMAR      ORG: NP12     DATE:1999-07-01
C
C ABSTRACT: TO GET LATITUDE/LONGITUDE FROM THE GIVEN I/J COORDINATES.
C
C PROGRAM HISTORY LOG:
C   YY-MM-DD  ORIGINAL AUTHOR SHIMOMURA.
C   88-07-25  GLORIA DENT  PUT IN DOCUMENTATION BLOCK
C   89-07-20  HENRICHSEN COMPLETE DOCUMENTATION,CLEAN UP AND PUT
C                        IN GRAPHICS SOURCE AND LOAD.
C   89-11-29  HENRICHSEN ADDED MORE DOCUMENTATION.
C   93-04-20  LILLY  CONVERT TO FORTRAN 77.
C   95-01-05  LUKE LIN   CONVERT IT CFT-77
C 1999-07-01  KRISHNA KUMAR CONVERTED THIS CODE FROM CRAY TO 
C             IBM RS/6000.
C
C USAGE:    CALL TRULL(XI,XJ,ALAT,ALONG,KEIL)
C
C   INPUT ARGUMENT LIST:
C     XI       - REAL*4 I COORDINATE OF POINT.
C     XJ       - REAL*4 J COORDINATE OF POINT.
C     KEIL     - INTEGER*4 FLAG TO DETERMINE MAP PROJECTION.
C              - FOR THE GIVEN I/J COORDINATES.
C              - =1  FOR LFM GRID
C              - =2  FOR N.HEM 1/40M 47*51 POLE(24,26) 80W VERT.
C              - =3  FOR SRN HEMI 381KM GRID INT., 80W IS VERT.
C              -     AT TOP, SRN HEMI HAVE LATS HAVE NEG. VALUES.
C              - =4  (USED FOR PEATMOS POP) 190.5 KM GRID LENGTH,
C              - =5 FOR SFC US 1/10M   23 FEB 73
C              - =6 FOR LARGER AREA US 1/10M      12 JUNE 1975 .
C              - =7 FOR LARGE NH 1/20M 105W FRONT 2/3   20 OCT 1975.
C              - =8 FOR LARGE NH 1/20M BACK PANEL SIDEWAYS 20 OCT 75.
C              - =9 FOR 65*65 N.HEMI 1/40M W/ 105W VERTICAL. JAN 16,76
C              - =10 FOR 47*51 N.HEMI 1/40M W/ 105W VERTICAL.JAN 16,76
C              - =11 FOR 51*51 LFM SUBSET OF 1/40M W/ 105W VERTICAL
C              -     1/16/76
C              - =12 FOR 53*57 FULL LFM GRID W/ 105W VERTICAL 7/22/76
C              - =13 FOR 43*31 LFM SUBSET W/ 105W VERTICAL  7/23/76
C              - =14 FOR 65*65 STD NMC GRID, NHEMI W/80W VERT 7/7/77
C              - = 15 FOR NA AFOS W/ 105W VERTICAL   16 MAR 1981.
C              - = 16 FOR LARGE SH 1/20M WITH 60W VERTICAL 30 APR 81.
C              - = 17 FOR 87*71 NH 1/20M WITH 105W VERTICAL 9 JUN 82
C              - =18 FOR 48*44 NH 1/20M W/102.5W VER
C              -      (DLY WEA MAP) 9/26/83.
C
C   OUTPUT ARGUMENT LIST:
C     ALAT     - REAL*4  LATITUDE OF I/J COORIDINATE.
C     ALONG    - REAL*4  LONGITUDE OF I/J COORDINATE.
C
C   OUTPUT FILES:
C     FT06F001 - PRINT FILE.
C
C REMARKS: RE ASSUMES 6371.2 KM EARTH RADIUS.
C   KEIL =1  VERT MERIDIAN IS 105W. (LFM)
C   KEIL =2  VERT MERIDIAN IS  80W.(STD NMC)
C   KEIL =3  VERT MERIDIAN IS 100E.(SRN)
C   KEIL =4  VERT MERIDIAN IS  98W. (PEATMOS)
C   KEIL=5 AND =6  VERT MERIDIAN IS 195W.
C   KEIL=7  VERT MERID IS 105W.
C   KEIL=8  VERT MERID IS 15W .
C   KEIL=9,10,11,12,13,15  HAVE VERT MERIDIAN AT 105W
C   KEIL=14 HAS VERT MERIDIAN AT 80W
C   KEIL=16 HAS VERT MERIDIAN AT 60W
C   KEIL=17 HAS VERT MERIDIAN AT 105W
C   KEIL=18 HAS VERT MERIDIAN AT 102.5W
C
C
C ATTRIBUTES:
C   LANGUAGE: F90
C   MACHINE:  IBM
C
C$$$
C
      REAL     ADDLN2(18)
      REAL     ALAT
      REAL     ALONG
      REAL     DEGPRD
C     ...DEGREES PER RADIAN
      REAL     GI2
      REAL     RE(18)
      REAL     TERMI
      REAL     TERMJ
      REAL     TLONG
      REAL     XI
      REAL     XIP(18)
      REAL     XJ
      REAL     XJP(18)
      REAL     XKSH
      INTEGER  MXKEIL
C     ...WHERE KEIL IS MAX NO. OF GRIDS THIS S/R WORKS FOR
      DATA     ADDLN2       / 15.0,-10.0, 10.0,  8.0,
     1                      2*105.0, 15.0, -75.0,5*15.0,
     2                       -10.0, 15.0, 30.0, 75.0, 77.5/
      DATA     DEGPRD       /57.29578/
      DATA     RE           /62.40866, 31.20433, 31.20433, 62.40866,
     1                       2*124.81733, 2*62.40866, 2*31.20433,
     2                       3*62.40866, 31.20433, 62.40866,
     3                       62.40866,  93.61299, 62.40866/
      DATA     XIP          /24.0,  24.0, 24.0,  24.0,
     1                      -35.0, -11.0, 55.0, -15.0,
     2                       33.0,  24.0, 26.0,  27.0,
     3                       17.0,  33.0, 27.0,  55.0,
     4                       40.0, 21.0/
      DATA     XJP          / 46.0, 26.0, 26.0, 46.0, 47.0, 47.0,
     1                        51.0, 55.0, 33.0, 26.0, 46.0, 49.0,
     2                        46.0, 33.0, 46.0, 65.0, 73.0, 48.0/
      DATA      MXKEIL      /18/
C
      XKSH = 1.0
      KEY = KEIL
      IF(KEY) 800,800,411
  411 CONTINUE
      IF(KEY .GT. MXKEIL) GO TO 800
C     ...OTHERWISE, KEIL IS W/I RANGE
      IF((KEY .EQ. 3) .OR. (KEY .EQ. 16)) XKSH = -1.0
      GI2 = RE(KEY) * RE(KEY)
      TERMI = XI - XIP(KEY)
      TERMJ = XJ - XJP(KEY)
      R2 = TERMI * TERMI + TERMJ * TERMJ
      IF(R2) 422,444,422
  422 CONTINUE
      TLONG = DEGPRD *  ATAN2(TERMJ,TERMI)
      ALONG = XKSH * (ADDLN2(KEY) - TLONG)
      IF(ALONG .LT. 0.0) ALONG = ALONG + 360.0
      ALAT = ASIN((GI2 - R2) / (GI2 + R2)) * DEGPRD
      ALAT = XKSH * ALAT
      RETURN
  444 CONTINUE
      ALAT = XKSH * 90.0
      ALONG = 0.0
      RETURN
  800 CONTINUE
C     ...COMES HERE IF GIVEN KEIL WAS OUT OF ALLOWABLE RANGE
      PRINT  802,KEIL
  802 FORMAT(1H1,10X,'ERROR STOP FROM TRULL. ARGUMENT OUT-OF-RANGE.  KEI
     XL = HEX ', Z8)
      STOP
      END
