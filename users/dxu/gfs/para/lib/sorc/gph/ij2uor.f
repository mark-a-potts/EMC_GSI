      SUBROUTINE IJ2UOR(XRELST,YRELST,XUOR,YUOR)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    IJ2UOR      CONVERT GRID COORDINATES TO UORS.
C   PRGMMR: KRISHNA KUMAR      ORG: W/NP12  DATE: 1999-07-01
C
C ABSTRACT: CONVERT GRID COORDINATES FROM NMC STD GRID RELATIVE
C   TO THE POLE, TO UNITS OF RESOLUTION (UOR) ON THE 1:10,000,000
C   POLAR STEREO GRAPHIC MAP, LIKE NORAMPO,
C   WHICH IS USING WORKING UNITS TO THE 1/1000TH OF CM.
C   LOGIC GETS THERE VIA LFM GRID RELATIVE TO THE POLE.
C   NMC STANDARD GRID IS ORIENTED AT 80W, THE STANDARD GRID INTERVAL
C   IS 381 KM. THE LFM FRID IS ORIENTED AT 105W, AND HAS A GRID
C   INTERVAL OF 190.5 KM.
C
C PROGRAM HISTORY LOG:
C   89-05-08  ORIGINAL AUTHOR SHIMOMURA
C   94-12-22  LUKE LIN      CONVERT IT TO CFT-77.
C   96-03-19  LUKE LIN      ADD NGM SUPER C-GRID.
C 1999-07-01  KRISHNA KUMAR CONVERTED THIS CODE FROM CRAY TO IBM RS/6000.
C
C USAGE     CALL IJ2UOR(XRELST,YRELST,XUOR,YUOR)
C   INPUT ARGUMENT LIST:
C     XRELST   - REAL*4 THE X GRID COORDINATES FROM NMC STD GRID
C              - RELATIVE TO THE POLE
C     YRELST   - REAL*4 THE Y GRID COORDINATES FROM NMC STD GRID
C              - RELATIVE TO THE POLE
C     XPOL     - REAL*4 X LOCATAION OF POLE FROM COMMON /POLE/
C     YPOL     - REAL*4 Y LOCATAION OF POLE FROM COMMON /POLE/
C     GDTYPE   - INTEGER*4 GRID TYPE NUMBER FROM COMMON /POLE/
C              - = 05 THEN THIS IS LFM 53X57 ANALSYS GRID.
C              - = 26 THEN THIS IS LFM 53X45 GRID.
C              - = 27 THEN THIS IS STANDARD NMC 65X65 GRID.
C              - = 101 THEN THIS IS THE NGM BIG CGRID 113X91.
C              - = 104 THEN THIS IS THE NGM SUPER CGRID 147X110.
C
C   OUTPUT ARGUMENT LIST:
C     XUOR     - REAL*4 X LOCATION IN UORS.
C     YUOR     - REAL*4 Y LOCATION IN UORS.
C
C   OUTPUT FILES:
C     FT06F001 - PRINT FILE.
C
C REMARKS: ONLY TWO GRID TYPES ARE ALLOWED: 26 AND 27.
C
C ATTRIBUTES:
C   LANGUAGE: F90
C   MACHINE:  IBM
C
C$$$
C
      COMMON  /POLE/ XPOL,YPOL,GDTYPE
      INTEGER     GDTYPE
C
C     ...THE POLE POSITION OF THE GRID(65,65) IS AT GRID(33,33).
C     ...THE POLE POSITION OF THE LFM GRID(53,45) IS AT GRID(27,49).
C
       REAL  XRELST,YRELST
       REAL  XUOR,YUOR
       REAL  XRELFM,YRELFM
C
       REAL  SINANG
C      ... SINANG = SIND(25.0)
       REAL  COSANG
C      ... COSANG = COSD(25.0)
       REAL  AMULT
C      ... WHERE AMULT = 381.0 / 190.5
       REAL  CONVUOR
C
       DATA    SINANG / 0.4226182 /
       DATA    COSANG / 0.9063077 /
       DATA    AMULT / 2.0 /
       DATA    CONVUOR / 1905.0 /
       DATA    CONVUOR1 /  914.52 /
       DATA    CONVUOR2 /  907.5464 /
C
       IF (GDTYPE .EQ. 27) THEN
C
C         THIS WAS THE STANDARD N.H. 65X65 GRID.
C
          CALL REROTA(XRELST,YRELST,SINANG,COSANG,AMULT,XRELFM,YRELFM)
C         WRITE(6,235)XRELST,YRELST,XRELFM,YRELFM
C 235     FORMAT(1H ,'RELROTA X,Y=',2F11.4';   XRLFM,YRLFM=',2F11.4)
          XUOR = XRELFM * CONVUOR
          YUOR = YRELFM * CONVUOR
C
C         XUOR = XRELST * CONVUOR * 2.0
C         YUOR = YRELST * CONVUOR * 2.0
C
       ELSE IF (GDTYPE .EQ. 26 .OR. GDTYPE.EQ.05) THEN
C
C         THIS WAS AN LFM GRID TYPE 53X45
C             OR
C         AN LFM GRID TYPE 53X57
C
          XUOR = XRELST * CONVUOR
          YUOR = YRELST * CONVUOR
       ELSE IF (GDTYPE .EQ. 101) THEN
C
C         THIS WAS A NGM C-GRID 113X91 
C
          XUOR = XRELST * CONVUOR1
          YUOR = YRELST * CONVUOR1
       ELSE IF (GDTYPE .EQ. 104) THEN
C
C         THIS WAS A SUPER NGM C-GRID 147X110
C
          XUOR = XRELST * CONVUOR2
          YUOR = YRELST * CONVUOR2
       ELSE
          WRITE(6,FMT='('' IJ2UOR: ERROR '',I3,'' IS A NON VALID'',
     1       '' GRID TYPE.'')')GDTYPE
       ENDIF
       RETURN
       END
